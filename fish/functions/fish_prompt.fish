# Defined interactively
function fish_prompt --description 'Write out the prompt'
    set -l last_pipestatus $pipestatus
    set -lx __fish_last_status $status # Export for __fish_print_pipestatus.

    set -g __fish_git_prompt_color_branch magenta --bold
    set -g __fish_git_prompt_char_stagedstate "+"
    set -g __fish_git_prompt_char_dirtystate "!"
    set -g __fish_git_prompt_char_untrackedfiles "â€¦"
    set -g __fish_git_prompt_char_invalidstate "x"
    set -g __fish_git_prompt_char_cleanstate ""
    set -g __fish_git_prompt_color_invalidstate red

    set -l color_cwd
    set -l suffix
    if functions -q fish_is_root_user; and fish_is_root_user
        if set -q fish_color_cwd_root
            set color_cwd $fish_color_cwd_root
        else
            set color_cwd $fish_color_cwd
        end
        set suffix '#'
    else
        set color_cwd $fish_color_cwd
        set suffix '>'
    end

    # PWD
    set_color $color_cwd
    echo -n (prompt_pwd)
    set_color normal

    printf '%s ' (fish_vcs_prompt)

    set -l pipestatus_string (__fish_print_pipestatus "[" "] " "|" (set_color $fish_color_status) (set_color --bold $fish_color_status) $last_pipestatus)
    echo -n $pipestatus_string
    set_color $color_cwd
    echo -n "$suffix "
    set_color normal
end
